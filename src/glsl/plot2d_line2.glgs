#version 430

layout(points) in;
layout(line_strip, max_vertices = 12) out;

flat in vec4 color_gs[];

flat out vec4 color_fs;

struct relation {
	ivec2 indices;
	vec4 line_a;
	vec4 line_b;
};

uniform relation relations[6];
uniform ivec2 resolution;
uniform float threshold;

vec2 to_window_space(in vec2 pos) {
	return 2.0 * (pos/resolution) - 1.0;
}

void emit_line(in vec4 p, in relation rel) {
	
	vec2 s = vec2(mix(rel.line_a.xy, rel.line_a.zw, 0.8*p[rel.indices.x] + 0.1));
	vec2 e = vec2(mix(rel.line_b.xy, rel.line_b.zw, 0.8*p[rel.indices.y] + 0.1));
	
	gl_Position = vec4(to_window_space(s), 0.0, 1.0);
	EmitVertex();
	gl_Position = vec4(to_window_space(e), 0.0, 1.0);
	EmitVertex();
	EndPrimitive();
}

void main() {
	color_fs = color_gs[0];

	vec4 p = gl_in[0].gl_Position;

	float avg = p[0] + p[1] + p[2] + p[3];
	avg *= 0.25f;

	if (avg > threshold) {
		color_fs = vec4(1.0, 0.0, 0.0, 1.0);//evaluate_transfer_function(p, 1.0);
		
		emit_line(p, relations[0]);
		emit_line(p, relations[1]);
		emit_line(p, relations[2]);
		emit_line(p, relations[3]);
		emit_line(p, relations[4]);
		emit_line(p, relations[5]);
	}
}
