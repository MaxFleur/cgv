#version 430

#define VALUE_TYPE_DEFINITION uint

layout(local_size_x = 256) in;

layout(std430, binding = 0) readonly buffer keys_in_buffer {
    uint keys_in[];
};

layout(std430, binding = 1) writeonly buffer keys_out_buffer {
    uint keys_out[];
};

layout(std430, binding = 2) readonly buffer values_in_buffer {
    VALUE_TYPE_DEFINITION values_in[];
};

layout(std430, binding = 3) writeonly buffer values_out_buffer {
    VALUE_TYPE_DEFINITION values_out[];
};

layout(std430, binding = 4) readonly buffer prefix_sum_buffer {
    uint prefix_sum[];
};

layout(std430, binding = 5) readonly buffer blocksum_buffer {
    uint blocksum[];
};

layout(std430, binding = 6) readonly buffer scratch_buffer {
    uint scratch[];
};

uniform uint n; // total number of items to sort
uniform uint n_blocksums; // total number of blocksums including padding
uniform uint last_blocksum_idx; // last non-padded blocksum index

void main() {

	uint tid = gl_LocalInvocationID.x;
	uint wid = gl_WorkGroupID.x;

	uint count[4];
	uint buckets_sum[4];

    uint last_prefix_sum = scratch[4];
        
    count[0] = (last_prefix_sum&0x000000FF) + blocksum[last_blocksum_idx];
	last_prefix_sum = last_prefix_sum >> 8;
	count[1] = (last_prefix_sum&0x000000FF) + blocksum[last_blocksum_idx + n_blocksums];
	last_prefix_sum = last_prefix_sum >> 8;
    count[2] = (last_prefix_sum&0x000000FF) + blocksum[last_blocksum_idx + n_blocksums + n_blocksums];
	
    count[scratch[5]] += 1;

    buckets_sum[0] = 0;
    buckets_sum[1] = count[0];
    buckets_sum[2] = count[0] + count[1];
    buckets_sum[3] = count[0] + count[1] + count[2];

	uint bid = wid;

    for(uint idx = wid*gl_WorkGroupSize.x + tid; idx < n; idx += gl_WorkGroupSize.x*gl_NumWorkGroups.x) {
		uint key = keys_in[idx];
        uint bucket = key&3;
        
        uint prefix_sum_value = (prefix_sum[idx>>2]>>(8*(idx&3)))&0x000000FF;
        
		uint blocksum_value = blocksum[bucket * n_blocksums + bid];

        uint scatter_addr = prefix_sum_value + blocksum_value + buckets_sum[bucket];

        keys_out[scatter_addr] = key>>2;
		values_out[scatter_addr] = values_in[idx];

		bid += gl_WorkGroupSize.x*gl_NumWorkGroups.x;
    }
}
